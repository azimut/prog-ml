```futhark
import "utils"

def features : [30][3]f64 = [[13.0, 26, 9], [2, 14, 6], [14, 20, 3], [23, 25, 9], [13, 24, 8], [1, 12, 2], [18, 23, 9], [10, 18, 10], [26, 24, 3], [3, 14, 1], [3, 12, 3], [21, 27, 5], [7, 17, 3], [22, 21, 1], [2, 12, 4], [27, 26, 2], [6, 15, 4], [10, 21, 7], [18, 18, 3], [15, 26, 8], [9, 20, 6], [26, 25, 9], [8, 21, 10], [15, 22, 7], [10, 20, 2], [21, 21, 1], [5, 12, 7], [6, 14, 9], [13, 19, 4], [13, 20, 3]]

def truths : [30][1]f64 = [[44.0], [23], [28], [60], [42], [5], [51], [44], [42], [9], [14], [43], [22], [34], [16], [46], [26], [33], [29], [43], [37], [62], [47], [38], [22], [29], [34], [38], [30], [28]]

def predict = matmul

def gradient [n] [m] (features: [n][m]f64) (truths: [n][1]f64) (weights: [m][1]f64) : [m][1]f64 =
  matsub (predict features weights) truths
  |> matmul (transpose features)
  |> matunary (* 2)
  |> matunary (/ (f64.i64 n))

def init_weights (m: i64) : [m][1]f64 =
  unflatten ((replicate m 0.0) :> [m * 1]f64)

def train [n] [m] (features: [n][m]f64) (truths: [n][1]f64) (iterations: i64) (lrate: f64) : [m][1]f64 =
  loop weights = init_weights m
  for _i < iterations do
    matsub weights (matunary (* lrate) (gradient features truths weights))

entry test = (\features pizzas -> train features pizzas 100000 0.001)

def main [n] [m] (features: [n][m]f64) (pizzas: [n][1]f64) : [][]f64 =
  let weights = #[trace] test features pizzas
  let predictions = predict features weights
  in loop result: [][]f64 = []
     for i < i64.min 5 (length pizzas) do
       result ++ [[predictions[i][0], pizzas[i][0]]]
```

With iters=100000 lrate=0.001
==
entry: test
compiled input @ data/pizza_2_vars_fix.txt
output { [[ 0.25032208429987246 ],[ 1.5632873204761812 ]] }
compiled input @ data/pizza_3_vars_fix.txt
output { [[ 1.1655612622487832  ],[ 0.14250111584252093 ],[ 3.099359590851483 ]] }


## Loss

```futhark
def loss [n] [m] (features: [n][m]f64) (truths: [n][1]f64) (weights: [m][1]f64) : f64 =
  matsub (predict features weights) truths
  |> matunary (** 2)
  |> flatten
  |> average
```

## Plots

```futhark
def pxys = (iota (length (flatten truths)), predict features (train features truths 10000 0.001) |> flatten)
def txys = (iota (length (flatten truths)), truths |> flatten)
```

```
> :gnuplot { truth=txys , prediction=pxys };
set title "04a.dimensions.fut"
set monochrome
unset border
set grid
plot truth with points pt 2, prediction with points pt 3
```

![](media/04a.dimensions-img/2a3f12c6830943ae6b0cc7cc81865b1e-plot.png)
